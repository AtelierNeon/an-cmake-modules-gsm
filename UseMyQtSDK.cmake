# This module is used to detect if there is a Qt SDK on host machine.
#
# Currently Qt 5 for Windows is supported.
message (STATUS "Detecting Qt SDK")
set (_QT_SDK_ROOT ${BUILD_WITH_QT_SDK_ROOT})
if (NOT _QT_SDK_ROOT)
  set (_QT_SDK_VERSION ${BUILD_WITH_QT_VERSION})
  set (_QT_SDK_PLATFORM ${BUILD_WITH_QT_PLATFORM})
  if (NOT _QT_SDK_PLATFORM)
    if ("${MSVC_TOOLSET_VERSION}" STREQUAL "142" AND "${CMAKE_SIZEOF_VOID_P}" STREQUAL "4")
      set (_QT_SDK_PLATFORM "msvc2019")
    elseif ("${MSVC_TOOLSET_VERSION}" STREQUAL "142" AND "${CMAKE_SIZEOF_VOID_P}" STREQUAL "8")
      set (_QT_SDK_PLATFORM "msvc2019_64")
    elseif ("${MSVC_TOOLSET_VERSION}" STREQUAL "143" AND "${CMAKE_SIZEOF_VOID_P}" STREQUAL "4")
      set (_QT_SDK_PLATFORM "msvc2022")
    elseif ("${MSVC_TOOLSET_VERSION}" STREQUAL "143" AND "${CMAKE_SIZEOF_VOID_P}" STREQUAL "8")
      set (_QT_SDK_PLATFORM "msvc2022_64")
    endif ()
  endif ()
  if (_QT_SDK_VERSION AND _QT_SDK_PLATFORM)
    set (_QT_SDK_ROOT "C:/Qt/${_QT_SDK_VERSION}/${_QT_SDK_PLATFORM}")
  endif ()
endif ()
if (NOT _QT_SDK_ROOT)
  message (STATUS "Detecting Qt SDK - NOT FOUND")
else ()
  message (STATUS "Detecting Qt SDK - ${_QT_SDK_ROOT}")
  list (APPEND CMAKE_PREFIX_PATH ${_QT_SDK_ROOT})

  set (_QT_SDK_SEARCH_PATH "${_QT_SDK_ROOT}/bin")
  message (STATUS "Detecting Qt deploying tool")
  find_program (_QT_DEPLOY_EXECUTABLE_PATH
      NAMES windeployqt
      PATHS ${_QT_SDK_SEARCH_PATH})
  if (NOT _QT_DEPLOY_EXECUTABLE_PATH)
    message (STATUS "Detecting Qt deploying tool - NOT FOUND")
  else ()
    message (STATUS "Detecting Qt deploying tool - ${_QT_DEPLOY_EXECUTABLE_PATH}")
    set (QT_DEPLOY_COMMAND ${_QT_DEPLOY_EXECUTABLE_PATH})
  endif ()
endif ()
